apply from:"gradle/knn.gradle"

def hasDefaults = rootProject.file("gradle.properties").exists()

configure(rootProject) {
    task localSettings() {
        doFirst {
            // If we don't have the defaults yet, create them.
            if (hasDefaults) {
                logger.lifecycle("Local settings already exist, skipping generation.")
            } else {
                def parentDir = rootProject.projectDir.toPath()
                        .resolve("../lucene_candidate").toAbsolutePath().toString()
                rootProject.file("gradle.properties").write("""
# These settings have been generated automatically on the first run.
external.lucene.repo=$parentDir
lucene.version=11.0.0
""", "UTF-8")
                throw new GradleException("IMPORTANT. This is the first time you ran the build. " +
                        "I wrote some sane defaults (for this machine) to 'gradle.properties'. " +
                        "Rerun the command to apply the properties.")
            }
        }
    }
}

if (hasDefaults == false) {
    // Make all tasks depend on local setup to make sure it'll run.
    allprojects {
        tasks.all { task ->
            if (task != rootProject.localSettings) {
                task.dependsOn rootProject.localSettings
            }
        }
    }
}

allprojects {
    tasks.withType(Exec) {
        doFirst {
            println "cmd: $commandLine"
        }
    }
}